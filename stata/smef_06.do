clear
set more off
cd /Users/polinapogorelova/Desktop/СМЭФ_Эконометрика
log using sem6.log

* Обозначения в комментариях (соответсвуют обозначениям из лекции):
	*              y* = xb + u - нецензурированная переменная, y - цензурированная переменная

* Tobit I. Цензурированная регрессия. Пример 1 (данные о дополнительных выплатах сотрудникам)
use http://fmwww.bc.edu/ec-p/data/wooldridge/fringe.dta, clear

sum annbens exper age educ tenure married male // описательные статистики

* Оценим сперва с помощью МНК модель регрессии для ежегодных дополнительных выплат
reg annbens exper c.exper#c.exper age annearn tenure // линейная модель регрессии, оцененная с помощью МНК
predict yhat_lm // прогноз доп. выплат согласно линейной модели регрессии

* Так как зависимая переменная цензурирована снизу нулем, оценим tobit модель 
tobit annbens exper c.exper#c.exper age annearn tenure, ll(0) // тобит-модель для данных, цензурированных слева нулем
* Интерпретация оценок коэффициентов в таблице на примере переменной age:
	* увеличение возраста на 1 год приводит к уменьшению в среднем на -26.40167 денежных единиц 
	* нецензурированных дополнительных выплат сотруднику
predict yhat_tobit // прогноз доп. выплат согласно tobit-модели

* Рассчитаем предельные эффекты
margins, dydx(*) predict(ystar(0,.)) // предельные эффекты для мат. ожидания цензурированной переменной
margins, dydx(*) predict(e(0,.)) // предельный эффект для мат. ожидания латентной переменной (annbens>0) !!!
margins, dydx(*) predict(pr(1000,2000)) // предельный эффект для Pr(a < y* < b) - вероятности того, что нецензурированная переменная принадлежит интервалу (a,b)




* Tobit II (Heckman model). Пример 2. Данные MROZ.
use http://www.stata.com/data/jwooldridge/eacsap/mroz, clear

tab inlf // таблица частот для переменной inlf
sum lwage if inlf == 1 // описательные статистики зарплаты работающих женщин

* Обычная модель линейной регрессии для логарифма зарплаты
reg lwage educ exper c.exper#c.exper, vce(robust)

* Оценивание модели Хекмана с помощью ММП
heckman lwage educ exper c.exper#c.exper, select(inlf = educ exper age nwifeinc c.exper#c.exper kidslt6 kidsge6) 

* Интерпретация оценок коэффициентов на примере переменной age (аналогично можно получить с помощью команды margins
* в строке 78 данного do-файла):
*      дополнительный год образования увеличивает зарплату на 10.84 процентных пункта

* Sigma - стандартная ошибка остатков в уравнении зарплаты (уравнение интенсивности участия)
* Rho - коэффициент корреляции между ошибками в уравнении для участия и для интенсивности участия
* Lambda = rho*sigma - эффект самоотбора

* Внизу таблицы с результатами приведены результаты тестирования гипотезы H0: rho = 0 с помощью LR теста.
* Так как p-value 0.8577, то H0 не отвергается на уровне значимости 5%, таким образом, можно оценивать модель с помощью МНК.

* Оценивание модели Хекмана с помощью двухшаговой процедуры
* Двухшаговая процедура может быть воспроизведена автоматически с помощью опции twostep
heckman lwage educ exper c.exper#c.exper, select(inlf = educ exper c.exper#c.exper age nwifeinc kidslt6 kidsge6) twostep

* Прогнозирование
* ycond прогнозирует ожидаемое значение логарифма зарплаты при условии, что зарплата наблюдаема, т.е. E(y*|y* was observed)
predict wage_without, ycond
sum lwage wage_without if lwage != . // сравним с выборочным значением
 
* yexpected прогнозирует ожидаемое значение для логарифма зарплаты для всех женщин,
* заменяя нулями отсутствующие значения для неработающих женщин
predict wage_all, yexpected
* сравним с выборочным значением
gen lwage0 = lwage
replace lwage0 = 0 if lwage == .
summarize wage_all lwage0

* psel прогнозирует вероятность того, что зависимая переменная будет наблюдаема (вероятность того, что женщина будет работать)
predict prob_model, psel
sum prob_model

* Предельные эффекты
* предельные эффекты образования и стажа в уравнении интенсивности (размера зарплаты) для нецензуированной переменной y*
margins, dydx(educ exper)

* предельные эффекты для уравнения отбора - влияние объясняющих переменных из уравнения отбора на вероятность того, что inlf = 1
margins, dydx(*) predict(psel)

* предельные эффекты образования и стажа (из уравнения для зарплаты) для ожидаемого значения цензурированной переменной y
margins, dydx(educ exper) predict(ystar(0,.))
